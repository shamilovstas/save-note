plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'androidx.room'
    id 'jacoco'
}

android {
    namespace 'com.shamilovstas.text_encrypt'
    compileSdk 34

    defaultConfig {
        applicationId "com.shamilovstas.text_encrypt"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
    kapt {
        correctErrorTypes true
    }

    packaging {
        resources {
            it.excludes += "/META-INF/{AL2.0,LGPL2.1}"
            it.merges += "META-INF/LICENSE.md"
            it.merges += "META-INF/LICENSE-notice.md"
        }
    }

    room {
        schemaDirectory "$projectDir/schemas"
    }

    testOptions {
        packagingOptions {
            jniLibs {
                useLegacyPackaging = true
            }
        }
        emulatorControl {
            enable = true
        }
    }
}

tasks.register('jacocoReport', JacocoReport) {
    dependsOn 'testDebugUnitTest'
    reports {
        csv { enabled false } // change if needed
        xml { enabled false } // change if needed
        html {
            enabled true
            destination file("${buildDir}/coverage-report")
        }
    }

    // Setup the .class, source, and execution directories
    final fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', 'android/**/*.*']

    // Include this if you use Kotlin
    final kotlinTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    final javacTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: fileFilter)
    final mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom files([mainSrc])
    classDirectories.setFrom files([kotlinTree, javacTree])
    executionData.setFrom fileTree(dir: project.buildDir, includes: [
            '**/*.exec', '**/*.ec'
    ])
}

dependencies {

    def room_version = "2.6.1"
    def nav_version = "2.7.5"
    def lifecycle_version = "2.6.2"
    def fragment_version = "1.8.1"
    def mockk_version = "1.13.11"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation("io.coil-kt:coil:2.5.0")
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "com.google.dagger:hilt-android:2.51"
    kapt "com.google.dagger:hilt-compiler:2.51"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.fragment:fragment-ktx:1.6.2"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.dagger:hilt-android-testing:2.51'
    testImplementation "io.mockk:mockk:${mockk_version}"
    kaptTest 'com.google.dagger:hilt-android-compiler:2.51'

    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.51'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.51'
    androidTestImplementation "io.mockk:mockk-android:${mockk_version}"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    debugImplementation("androidx.fragment:fragment-testing-manifest:$fragment_version")
    androidTestImplementation("androidx.fragment:fragment-testing:$fragment_version")
    androidTestImplementation 'androidx.test.espresso:espresso-device:1.0.1'
}